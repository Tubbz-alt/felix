<html>
<head>
<style>
table {
    font-family: arial, sans-serif;
    border-collapse: collapse;
    width: 100%;
}

td, th {
    border: 1px solid #dddddd;
    text-align: left;
    padding: 8px;
}

tr:nth-child(even) {
    background-color: #dddddd;
}
pre {
  background-color: rgb(210,255,210);
  margin-left: 20pt;
  margin-right: 20pt;
  padding-left: 10pt;
  padding-top: 5pt;
  padding-bottom: 5pt;
}
</style>
</head>
<body>
<h1>Felix Programming language</h1>
<p> Under construction</p>
<img src="FelixWork.jpg">
<h1>Felix</h1>

An advanced, statically typed, high performance scripting language with native C++ embedding.

<h2>Features</h2>
<h3>Autobuilder</h3>
This file:
<pre>
// hello.flx
println$ "Hello World";
</pre>

can be run directly:

<pre>bash
flx hello.flx
</pre>

It <it>works</it>. No makefiles. No compiler switches.
Automatic caching and dependency checking for Felix and C++.
Uses a *flx_pkgconfig* database consisting of a directory
of `*.fpc` files to specify and find libraries and header
files based on in language abstract keys.

<h3>Hyperlight Performance</h3>

The aim is to run faster than C.

Underneath Felix generates highly optimised machine
binaries which outperform most interpreters, bytecode compilers,
virtual machines, and sometimes compiled languages including C and C++.

Felix is an aggressive inliner which performs whole program
analysis and high level optimisations such as parallel assignment,
self-tail call elimination.

Felix generates optimised C++ which is then compiled and optimised
again by your system C++ compiler.

<table>
<tr><th>Compiler     <th>Ack   <th> Takfp
<tr><td>Felix/clang  <td>  3.71  <td>  6.23
<tr><td>Clang/C++    <td>  3.95  <td>  6.29
<tr><td>Felix/gcc    <td>  2.34  <td>  6.60
<tr><td>Gcc/C++      <td>  2.25  <td>  6.25
<tr><td>Ocaml        <td>  2.93  <td>  8.41
</table>

<h3>C and C++ embedding</h3>

Felix is a C++ code generator specifically designed so that 
all your favourite C and C++ libraries can be embedded
with little or no glue logic:

<pre>
// required header 
header vector_h = '#include <vector>';

// C++11 for smart pointers
header memory_h = '#include <memory>' 
  requires package "cplusplus_11"
;
 
// types
type vector[T] = "::std::shared_ptr<::std::vector<?1>>" 
  requires vector_h, memory_h
;

type viterator[T] = "::std::vector<?1>::iterator"
  requires vector_h
;

// constructor
ctor[T] vector[T] : unit = "::std::make_shared<::std::vector<?1>>()";

// operations
proc push_back[T] : vector[T] * T =  "$1->push_back($2);";
proc push_back[T] (v: vector[T]) (elt:T) => push_back(v,elt);

fun stl_begin[T] : vector[T] -> viterator[T] = "$1->begin()";
fun deref[T] : viterator[T] -> T = "*$1";

// example use
var v = vector[int]();
v.push_back 42;
println$ *v.stl_begin;
</pre>

<h3>Overloading</h3>

Ad hoc polymorphism.

<pre>
// overloads
fun f (x:double) => x +42.1;
fun f (x:int) =>  x + 1;
fun f (x:string) => x + "!";
</pre>

<h3>Simple Generics</h3>

Just don't give the argument type.

<pre>
// generics
fun g (x) => f (f x);
println$ g 1, g "hello";
println$ _map f (1,"hello",2.0);
</pre>

<h3>Type Classes</h3>

A better way of overloading:

<pre>
class Eq[T] {
  virtual fun == T * T -> T;
  fun != (x:T, y:T) => not (x == y);
}
instance Eq[int] {
  fun == : int * int -> int = "$1==$2"; //from C++
}
</pre>

<h3>Pattern matching</h3>

<pre>
match x with
| Some x => println$ x; 
| None => println "NONE";
endmatch;
</pre> 

<h3>Pointers</h3>

The only way to store a value. Felix has no references or
lvalues. Pointers are better.

<pre>
var x = 1;
&x <- 2;
</pre>


<h3>Type System based on Algebra</h3>

Felix uses a category theoretic model
of types which includes products (tuples etc), coproducts 
(variants etc), exponentials (closures), recursion, and pointers,
<it>as well</it> as providing both (unenforced) purely functional
(declarative) handling of inductive types and
and purely cofunctional (control flow) handling of
cofunctional types.


<h3>Purely Functional Programming </h3>

With <it>parametric polymorphism</it>, <it>higher order functions</it>,
<it>lexically scoped closures</it>, and <it>garbage collection</it>.
Mutation is still possible by use of <it>uniqueness typing</it>
which provides <it>move semantics</it>.

Felix has both C++ style _ad hoc polymorphism_ with
overloading and also Haskell style <it>type classes</it>,
(what would be called <it>concepts</it> in C++).

<pre>
var out = fold_left 
  (fun (acc:int) (elt:int) => acc + elt) 
  0
  ([1,2,3,4])
;
  
</pre>

<h3>Coroutines </h3>

In Felix imperative programming is done with statements
and procedures, but procedures are a special case
of coroutines. Any unit procedure can be spawned
as a <it>fibre</it> or <it>lightweight thread</it> which communicates
with other fibres using <it>synchronous channels</it>.


<h3>User Domain Specific Sub-Languages</h3>

The Felix grammar is part of the library.
The programmer can design new syntax as desired
using standard EBNF grammar rules, with action
codes written in R5RS Scheme which transform the
non-terminal arguments into arbitrary S-expressions.
These S-expressions are reduced to standard Felix AST terms.

Combining the syntax extension ability with library code
and C++ embedding allows definition of an _integrated_ Domain Specific
__sub__-language of Felix. 

<h3>Regular Definition DSSL</h3>

The following 
code uses library _combinators_ and embedded Google RE2
binding to specify a C identifier (with only `x` for letters
and `9` for digits):

<pre>
var digit = Charset "9";
var letter = Charset "x";
var us = Regdef::String "_";
var id = Seqs 
  ([   
     Alts ([us,letter]),
     Rpt( Alts([letter,digit,us]), 0,-1)
  )]
;
</pre>
With the standard regexp grammar we can generate the
combinators and thus calls to Google RE2 using 
the regexp DSSL:

<pre>
regdef digit = "9";
regdef letter = "x";
regdef us = "_";
regdef id = (us|letter)(letter|digit|us)*;
</pre>

which is much better than the string form:
<pre>
(?:\x5F|[x])(?:[x]|[9]|\x5F)*
</pre>

<h3>Chips and Circuits DSSL</h3>
This DSSL provides a syntactic model of coroutines as chips,
and the topology of channel connections between these chips
as circults. The code below is an advanced combination
of this model, pipelines, and uses the fixpoint operator
for recursion, to build a simple parser.

<pre>
include "std/strings/recognisers";
include "std/control/chips";

open BaseChips;
open Recognisers;

device L = match_string "(";
device R = match_string ")";
device E = match_string "E";

// Grammar:
// p = epsilon
// p = (p)p
// s = pE
var p = fix (proc (q:iochip_t[Buffer,Buffer]) 
  (io: (
    inp: %<Buffer,
    out: %>Buffer
  )) ()
 {
   device y = 
     tryall_list ([
       epsilon[Buffer],
       L |-> q |-> R |-> q
     ])
   ;
   circuit
     wire io.inp to y.inp
     wire io.out to y.out
   endcircuit
});

device parens = p |-> E;

device sayresult = procedure (proc (x:Buffer) {
  println$ "Test: End pos=" + x.str; })
;

device tests = source_from_list (["(()(()))E", "E", "()E"]);
device toBuffer = function (fun (s:string)=> Buffer s);

#(tests |-> toBuffer |-> parens |-> sayresult);

</pre>

<h3>Graphics</h3>

Felix has a builtin library for GUIs based on SDL2:

![Felix the cat](/src/web/images/graphics_demo.jpg)

<h2>Getting Started</h2>

<h3>Prerequisites</h3>

* Python 3
* Ocaml 4.06.1 (only for source build)
* C++ compiler: g++, clang++, or msvc

<h3>Extras (can be installed later)</h3>

* SDL2 for graphics
* GNU GMP, GNU GSL 

<h3>Build from Source</h3>

<h4>Linux</h4>

<pre>
git clone https://github.com/felix-lang/felix.git
cd felix
. buildscript/linuxsetup.sh
make  
sudo make install <h1>optional!</h1>
</pre>

<h4>OSX</h4>


<pre>
git clone https://github.com/felix-lang/felix.git
cd felix
. buildscript/macosxsetup.sh
make  
sudo make install <h1>optional!</h1>
</pre>

<h4>Windows</h4>
Make sure git, Python3 and Ocaml are on your PATH.
You can download a pre-built [Ocaml 4.06.1 for Windows](https://github.com/felix-lang/win64ocaml).

Open a cmd.exe console with Visual Studio 2015 or above
environment established or run vcvarsall x86. See [vcvarsall](https://msdn.microsoft.com/en-us/library/f2ccy3wt.aspx).

<pre>
git clone https://github.com/felix-lang/felix.git
cd felix
. buildscript/winsetup.sh
nmake  
nmake install <h1>optional!</h1>
</pre>

<h2>Tarballs</h2>

<http://github.com/felix-lang/felix/releases>

<h1>Build Status</h1>

Appveyor, Windows build: <img src=https://ci.appveyor.com/api/projects/status/q9w45r6b2chnsre1?svg=true> https://ci.appveyor.com/project/skaller/felix
Travis, Linux build: <img src=https://travis-ci.org/felix-lang/felix.svg?branch=master> https://travis-ci.org/felix-lang/felix

<h1>Links </h1>

<table>
<tr><th>Title                                <th> URL
<tr><td>Documentation Master                 <td> <a href=http://felix-documentation-master.readthedocs.io/en/latest>http://felix-documentation-master.readthedocs.io/en/latest</a>
<tr><td>Felix Tutorial                       <td> <a href=http://felix-tutorial.readthedocs.io/en/latest>http://felix-tutorial.readthedocs.io/en/latest</a>
<tr><td>Installation and Tools Guide         <td> <a href=http://felix-tools.readthedocs.io/en/latest>=http://felix-tools.readthedocs.io/en/latest</a>
<tr><td>Felix Language Reference Manual      <td> <a href=http://felix.readthedocs.io/en/latest>http://felix.readthedocs.io/en/latest</a>
<tr><td>Felix Library Packages               <td> <a href=http://felix-library-packages.readthedocs.io/en/latest>http://felix-library-packages.readthedocs.io/en/latest</a>
<tr><td>Articles on Modern Computing         <td> <a href=http://modern-computing.readthedocs.io/en/latest>http://modern-computing.readthedocs.io/en/latest</a>
<tr><td>Felix Home Page                      <td> <a href=http://felix-lang.github.io/felix>http://felix-lang.github.io/felix</a>
<tr><td>Felix Wiki                           <td> <a href=https://github.com/felix-lang/felix/wiki>https://github.com/felix-lang/felix/wiki</a>
<tr><td>Git Repository                       <td> <a href=https://github.com/felix-lang/felix>https://github.com/felix-lang/felix</a>
<tr><td>Binary Download                      <td> <a href=http://github.com/felix-lang/felix/releases>http://github.com/felix-lang/felix/releases</a>
</table>

<h1>Mailing List</h1>

mailto:felix-lang@googlegroups.com


<h1>Licence</h1>

Felix is Free For Any Use (FFAU)/Public Domain.

</body>

